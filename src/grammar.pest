start               = _{ SOI ~ top_level* ~ EOI }
top_level           = _{ python_code | picture_definition | film } 

film                = { identifier ~ empty_paramaters? ~ "::" ~ film_picture ~ film_parameters? }
film_picture        = { identifier ~ parameters? }
film_parameters     = { "[" ~ film_parameter? ~ ("," ~ film_parameter)*  ~ "]"}
film_parameter      = { identifier ~ "=" ~ expression }

python_code         = ${ python_delimeter ~ WHITESPACE* ~ python_code_line* ~ WHITESPACE* ~ python_delimeter }
python_code_line    = { !python_delimeter ~ (!python_delimeter ~ ANY)*  }
python_delimeter    = _{ "***" }

picture_definition  = { standard_picture | picture_function | picture_selection  }

standard_picture    = { identifier ~ empty_paramaters? ~ "<<" ~ basis_picture ~ (transform_set | csg_operation)+ }
basis_picture       = { identifier ~ parameters? }
transform_set       = { "[" ~ num_pics ~ transform_expressions? ~ transform* ~ "]" }
transform_expressions = { "(" ~ WHITESPACE* ~ (expression | "," | ";")* ~ ")" }
num_pics            = { "{" ~ expression ~ (";" ~ num_pics_identifier)? ~ (";" ~ num_pics_identifier)? ~ "}" }
num_pics_identifier = _{ identifier | blank_argument }
transform           = _{ scale_transform | translate_transform | rotate_transform | color_transform }
scale_transform     = { "*" ~ transform_arguments }
translate_transform = { "+" ~ transform_arguments }
rotate_transform    = { "@" ~ transform_arguments }
color_transform     = { "^" ~ transform_arguments }

transform_arguments = _{ "(" ~ transform_argument ~ "," ~ transform_argument ~ "," ~ transform_argument ~ ")" }
transform_argument  = _{ expression | blank_argument }
blank_argument      = { WHITESPACE* }

// balanced delimiters, commas only in delimiters
expression_unnested = _{ (!("(" | ")" | "{" | "}" | "[" | "]" | "," | ";") ~ ANY)+ }
expression_round    = _{ "(" ~ (expression | "," | ";")* ~ ")" } 
expression_square   = _{ "[" ~ (expression | "," | ";")* ~ "]" } 
expression_curly    = _{ "{" ~ (expression | "," | ";")* ~ "}" } 
expression          = { (expression_unnested | expression_round | expression_square | expression_curly)+ }

picture_function    = { identifier ~ parameters? ~ "<<" ~ basis_picture ~ transform_set* }

picture_selection   = { identifier ~ empty_paramaters? ~ "<<" ~ picture_list }

picture_list        = _{ "[" ~ picture_list_item? ~ ("," ~ picture_list_item)* ~ "]" }
picture_list_item   = _{ identifier }

csg_operation       = { csg_operator ~ csg_operand }
csg_operand         = _{ identifier ~ parameters? }
csg_operator        = { "+" | "-" | "n" | "&" }

parameters          = { "(" ~ parameter? ~ ("," ~ parameter)* ~ ")" }
parameter           = _{ named_expression | expression }
named_expression    = { identifier ~ "=" ~ expression }
empty_paramaters    = _{ "(" ~ ")" }

literal             = { number }
identifier          = @{ XID_START ~ XID_CONTINUE* }
number              = { ("." | NUMBER)+ } 

WHITESPACE          = _{ " " | "\t" | NEWLINE }
COMMENT             = _{ single_line_comment | multi_line_comment }
single_line_comment = _{ "#" ~ (!NEWLINE ~ ANY)* ~ (NEWLINE | EOI) } 
multi_line_comment  = _{ "///" ~ (!"\\\\\\" ~ ANY)* ~ "\\\\\\" } 

todo                = { "##TODO##" }